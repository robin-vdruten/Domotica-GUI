#
# Dependencies
#
include(FetchContent)

# GLFW
find_package(glfw3 3.4 QUIET)
if (NOT glfw3_FOUND)
    FetchContent_Declare(
            glfw3
            DOWNLOAD_EXTRACT_TIMESTAMP OFF
            URL https://github.com/glfw/glfw/releases/download/3.4/glfw-3.4.zip
    )
    FetchContent_GetProperties(glfw3)
    if (NOT glfw3_POPULATED)
        set(FETCHCONTENT_QUIET NO)
        FetchContent_Populate(glfw3)
        add_subdirectory(${glfw3_SOURCE_DIR} ${glfw3_BINARY_DIR})
    endif()
endif()

# OpenGL
find_package(OpenGL REQUIRED)

# GLAD
FetchContent_Declare(
    glad
    DOWNLOAD_EXTRACT_TIMESTAMP OFF
    URL https://github.com/Dav1dde/glad/archive/refs/tags/v2.0.8.zip
)

FetchContent_GetProperties(glad)
if(NOT glad_POPULATED)
    set(FETCHCONTENT_QUIET NO)
    FetchContent_Populate(glad)

    add_subdirectory("${glad_SOURCE_DIR}/cmake" glad_cmake)
    glad_add_library(glad REPRODUCIBLE EXCLUDE_FROM_ALL LOADER API gl:core=4.6)
endif()
set_target_properties(glad PROPERTIES FOLDER "Dependencies")

# GLM
find_package(PkgConfig QUIET)
if (PKG_CONFIG_FOUND)
    pkg_check_modules(GLM QUIET glm>=1.0.1)
    if (GLM_FOUND)
        message(STATUS "GLM found by pkg-config")
        add_library(glm INTERFACE)
        target_include_directories(glm INTERFACE ${GLM_INCLUDE_DIRS})
    endif()
else()
    find_package(glm 1.0.1 QUIET)
    if (glm_FOUND)
        message(STATUS "GLM found by find_package")
    endif()
endif()
if (NOT glm_FOUND AND NOT GLM_FOUND)
    FetchContent_Declare(
            glm
            DOWNLOAD_EXTRACT_TIMESTAMP OFF
            URL https://github.com/g-truc/glm/archive/refs/tags/1.0.1.zip
    )
    FetchContent_GetProperties(glm)
    if (NOT glm_POPULATED)
        message(STATUS "GLM found by FetchContent")
        set(FETCHCONTENT_QUIET NO)
        FetchContent_Populate(glm)
        add_library(glm INTERFACE)
        target_include_directories(glm INTERFACE ${glm_SOURCE_DIR})
    endif()
endif()

set_target_properties(glm PROPERTIES FOLDER "Dependencies")

# ImGui
FetchContent_Declare(
    imgui
    DOWNLOAD_EXTRACT_TIMESTAMP OFF
    URL https://github.com/ocornut/imgui/archive/refs/tags/v1.91.6-docking.zip
)

FetchContent_GetProperties(imgui)
if(NOT imgui_POPULATED)
    set(FETCHCONTENT_QUIET NO)
    FetchContent_MakeAvailable(imgui)
    
    # Create ImGui library
    add_library(imgui STATIC
        ${imgui_SOURCE_DIR}/imgui.cpp
        ${imgui_SOURCE_DIR}/imgui_demo.cpp
        ${imgui_SOURCE_DIR}/imgui_draw.cpp
        ${imgui_SOURCE_DIR}/imgui_tables.cpp
        ${imgui_SOURCE_DIR}/imgui_widgets.cpp
        ${imgui_SOURCE_DIR}/backends/imgui_impl_glfw.cpp
        ${imgui_SOURCE_DIR}/backends/imgui_impl_opengl3.cpp
    )
    
    target_include_directories(imgui PUBLIC 
        ${imgui_SOURCE_DIR}
        ${imgui_SOURCE_DIR}/backends
    )
    
    target_link_libraries(imgui PUBLIC glfw glad)
endif()

set_target_properties(imgui PROPERTIES FOLDER "Dependencies")

# ImPlot
FetchContent_Declare(
    implot
    DOWNLOAD_EXTRACT_TIMESTAMP OFF
    URL https://github.com/epezent/implot/archive/refs/tags/v0.16.zip
)

FetchContent_GetProperties(implot)
if(NOT implot_POPULATED)
    set(FETCHCONTENT_QUIET NO)
    FetchContent_MakeAvailable(implot)
    
    # Create ImPlot library
    add_library(implot STATIC
        ${implot_SOURCE_DIR}/implot.cpp
        ${implot_SOURCE_DIR}/implot_items.cpp
        ${implot_SOURCE_DIR}/implot_demo.cpp
    )
    
    target_include_directories(implot PUBLIC ${implot_SOURCE_DIR})
    target_link_libraries(implot PUBLIC imgui)
endif()

set_target_properties(implot PROPERTIES FOLDER "Dependencies")

